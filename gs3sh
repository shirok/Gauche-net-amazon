#!/usr/bin/env gosh
(use net.amazon.s3)
(use gauche.parseopt)
(use gauche.parameter)
(use gauche.sequence)
(use srfi-1)
(use srfi-13)
(use srfi-19)
(use util.match)
(use util.list)
(use file.util)
(use sxml.sxpath)

(define use-proxy? #f)

(define (main args)
  (let-args (cdr args) ([conf-file "c|conf=s"]
                        [else _ (usage)])
    (aws-read-config conf-file)
    (unless (aws-keys-ready?)
      (exit 1 "Access keys unknown.  Specify config file by --conf, or \
               create ~~/.awsconfig."))
    (run-cli))
  0)

(define (usage)
  (print "Usage: ,*program-name* [-c=config]")
  (exit 0))

(define *commands* '())

(define *current-bucket* #f)

(define-macro (define-command entry help . body)
  (match-let1 (name . formals) entry
    (let ([e (gensym)]
          [return (gensym)])
      `(push! *commands*
              `(,',name
                ,(lambda ,formals
                   (let/cc ,return
                     (define (command-usage)
                       (print "Usage: " ,help)
                       (,return #f))
                     (guard (,e [(<parseopt-error> ,e)
                                 (print (~ ,e'message))
                                 (command-usage)]
                                [else
                                 (print "ERROR: " (~ ,e'message))
                                 (,return #f)])
                       ,@body)))
                ,',help)))))

(define (dispatch line)
  (match (parse-cmdline line)
    [(name . args)
     (cond [(assq name *commands*) => (^p (apply (cadr p) args))]
           [else (print #`"unknown command: ,|name|.  Try 'help'.")])]
    [_ #f]))

(define (prompt)
  (if *current-bucket*
    (format #t "s3sh[~a]> " *current-bucket*)
    (format #t "s3sh> "))
  (flush))

(define (run-cli)
  (when use-proxy? (setup-proxy))
  (let loop ()
    (prompt)
    (let1 line (read-line)
      (unless (eof-object? line)
        (dispatch line)
        (loop)))))

(define (parse-cmdline line)
  ;; TODO: ultimately we want to parse like shell.  For now, the following
  ;; at least enables to handle double-quoted words:
  (guard (e [(<read-error> e)
             (print "command syntax error (~a)" (e'message))
             #f])
    (match (read-from-string #`"(,line)")
      [(word . rest) (cons word (map x->string rest))]
      [word word])))

(define (ls-date date)
  (if (time>=? (time-difference (current-time) (date->time-utc date))
               (make-time time-duration 0 (* 86400 (/ 365 2))))
    (date->string date "~b ~e  ~Y")
    (date->string date "~b ~e ~H:~M")))

(define (parse-path path)
  (receive (bucket name) (string-scan path "//" 'both)
    (cond [(not bucket) (values 'local path)]
          [(string-null? bucket)
           (unless *current-bucket* (no-bucket))
           (values *current-bucket* name)]
          [else (values bucket name)])))

;;;
;;; Commands
;;;

;;
;; Miscellaneous
;;

(define-command (help . args)
  "help [command]\n\
   Without command, lists the available commands and prints generic helps.\n\
   With command, show the detailed help of the command."
  (match args
    [() (help-generic)]
    [(cmd) (cond [(assq (string->symbol cmd) *commands*)
                  => (^p (print (caddr p)))]
                 [else (print #`"unknown command: ,|cmd|")])]
    [_ (command-usage)]))

(define (help-generic)
  (print
   "Pathname syntax:\n\
    - BUCKET//NAME specifies a remote object NAME in BUCKET.   If BUCKET is\n\
      omitted (i.e. the path begins with '//'), a current bucket is assumed.\n\
    - All other names are taken as local pathname, if it appears in a place\n\
      where object name is assumed.\n\
    Available commands (type help COMMAND for the details:")
  (dolist [entry (sort-by *commands* (.$ x->string car))]
    (format #t "  ~a\n" (with-input-from-string (caddr entry) read-line)))
  (print "Unrecognized commands are passed to shell."))

(define-command (pwd)
  "pwd\n\
   Print current (local) working directory."
  (print (sys-getcwd)))

(define-command (cd . args)
  "cd [directory]\n\
   Change (local) working directory.  If directotry is omitted, go to\n\
   home directory."
  (sys-chdir (get-optional args (home-directory)))
  (print (sys-getcwd)))

;;
;; Bucket
;;

(define-command (bucket . args)
  "bucket [-l] [[-acd] bucket-name]\n\
   Without arguments, list your buckets.  With bucket-name and no options,\n\
   set the current bucket to the given bucket-name.\n\
   Options:\n\
   -a bucket-name    See if the named bucket is available.\n\
   -c bucket-name    Create the named bucket.\n\
   -d bucket-name    Delete the named bucket.\n\
   -l [bucket-name]  Show auxiliary info of the named or current bucket."
  (let-args args ([avail "a=s"]
                  [create "c=s"]
                  [delete "d=s"]
                  [long   "l"]
                  [else _ (command-usage) '()]
                  . args)
    (cond
     [avail     (print "bucket "avail" is "(s3-bucket-availability avail))]
     [create    (s3-bucket-create! create) (print "bucket "create" created.")]
     [delete    (s3-bucket-delete! delete) (print "bucket "delete" deleted.")]
     [(null? args) (if long
                     (if *current-bucket*
                       (bucket-long *current-bucket*)
                       (no-bucket))
                     (map (lambda (e)
                            (match-let1 (name created) e
                              (format #t "~12a ~a\n" (ls-date created) name)))
                          (s3-bucket-list)))]
     [(not (null? (cdr args))) (command-usage)]
     [long      (bucket-long (car args))]
     [else      (set! *current-bucket* (car args))])))

(define (bucket-long name)
  (let ([location (s3-bucket-location name)]
        [acl (values-ref (s3-bucket-acl/raw name) 0)])
    (print "Bucket '"name"'")
    (print "    owner: "
           ((if-car-sxpath '(// aws:Owner aws:DisplayName *text*)) acl))
    (print " location: "location)
    (print " access granted to:")
    (dolist [grant ((sxpath '(// aws:Grant)) acl)]
      (let ([grantee ((if-car-sxpath '(// aws:DisplayName *text*)) grant)]
            [perm    ((if-car-sxpath '(// aws:Permission *text*)) grant)])
        (format #t "     ~15a  ~a\n" perm grantee)))))

(define (no-bucket :optional (msg ""))
  (errorf "No bucket specified~a" msg))

;;
;; Lister
;;

(define-command (ls . args)
  "ls [-l][-c COUNT][-s START] [pattern]\n\
  Lists remote objects and/or local files matching PATTERNs.  PATTERN can be\n\
  a remote object by BUCKET//NAME or //NAME, optionally followed by '*',\n\
  or just a glob pattern that are applied to local filesystem.\n\
  Options:
  -l        (long) Display more info per object.
  -c COUNT  Limit the number of listed objects by COUNT.  Only effective for
            remote objects
  -s START  Skip START objects before listing.  Only effective for remote
            objects."
  (let-args args ([long "l"]
                  [count "c=i"]
                  [start "s=i"] . pattern)
    (if (null? pattern)
      (ls-local (glob "*") long)
      (receive (bucket path) (parse-path (car pattern))
        (if (eq? bucket 'local)
          (ls-local (if (file-is-directory? path)
                      (glob #`",|path|/*")
                      (glob path))
                    long)
          (ls-remote bucket path long count start))))))

(define (ls-local files long)
  ;; TODO: support long option
  (ls-multicolumn files))

(define (ls-multicolumn files)
  (let* ([maxw (+ 1 (apply max (map string-length files)))]
         [cols (quotient 80 maxw)] ; TODO: check screen width
         [rows (ceiling (/ (length files) cols))]
         [colw (floor (/ 80 cols))])
    (let do-row ([collis (slices files rows #t)])
      (unless (every null? collis)
        (let do-column ([c collis])
          (match c
            [() (newline)]
            [((#f . _) . _) (newline)]
            [((x . _) . rest) (format #t "~va" colw x) (do-column rest)]))
        (do-row (map cdr collis))))))

(define (ls-remote bucket path long count start)
  (if long
    (ls-long-remote
     (s3-object-list bucket :prefix path :max-keys count))
    (ls-multicolumn
     (s3-object-list/key bucket :prefix path :max-keys count))))

(define (ls-long-remote objs)
  (let ([ow (apply max 5 (map (.$ string-length s3obj-owner-display-name) objs))]
        [sw (apply max 6 (map (.$ string-length x->string s3obj-size) objs))])
    (dolist [obj objs]
      (format #t "~a ~va ~vd ~12a ~a\n"
              (or (and-let* ([sc (s3obj-storage-class obj)])
                    (string-ref sc 0))            ;usually 'S' or 'R'
                  "-")
              ow (s3obj-owner-display-name obj)
              sw (s3obj-size obj)
              (ls-date (s3obj-last-modified obj))
              (s3obj-key obj)))))

;;
;; Getting object
;;

(define-command (cat . args)
  "cat [path ...]\n\
  Shows the content of object specified by PATHs."
  (dolist [arg args]
    (receive (bucket path) (parse-path arg)
      (if (eq? bucket 'local)
        (call-with-input-file path (cut copy-port <> (current-output-port)))
        (display (s3-object-get bucket path))))))

'(define (run-cli)
  (define (usage str)
    (display #`"USAGE: ,|str|\n" (current-error-port)))
  (define (warning str)
    (display #`"ERROR: ,|str|\n" (current-error-port)))
  (when use-proxy? (setup-proxy))
  (let ((bucket #f))
    (define (check-bucket)
      (or bucket (begin (warning "bucket is not set.") #f)))
    (let loop ()
      (display "sSh3ll> ") (flush)
      (let1 line (read-line)
        (if (eof-object? line)
            (print "quit")
            (begin (guard (e (else (warning (ref e 'message)) (raise e)))
                          (match (string-tokenize line)
                            (("bucket" bucketname)
                             (set! bucket bucketname)
                             (print #`"set bucket: ,|bucket|"))
                            (("bucket")
                             (print #`"now bucket is set to ,|bucket|"))
                            (("bucket" . args)
                             (usage "bucket [bucketname]"))

                            (("bucket-list")
                             (print (s3-bucket-list)))

                            (("bucket-available?")
                             (when (check-bucket)
                               (print (s3-bucket-availability bucket))))

                            (("bucket-location")
                             (when (check-bucket)
                               (print (s3-bucket-location bucket))))

                            (("createbucket")
                             (when (check-bucket)
                               (s3-bucket-create! bucket)))

                            (("deletebucket")
                             (when (check-bucket)
                               (s3-bucket-delete! bucket)))

                            (("list")
                             (when (check-bucket)
                               (format #t "~s\n" (s3-object-list bucket))))

                            (("list" prefix)
                             (when (check-bucket)
                               (format #t "~s\n"
                                       (s3-object-list bucket :prefix prefix))))

                            (("list" prefix max)
                             (when (check-bucket)
                               (format #t "~s\n"
                                       (s3-object-list bucket :prefix prefix
                                                       :max-keys max))))

                            (("put" id data . rem)
                             (when (check-bucket)
                               (s3-object-put! bucket id (tokens-drop line 2))
                               (print #`"put ,|id|")))

                            (("get" id)
                             (when (check-bucket)
                               (format #t "~s\n" (s3-object-get bucket id))))
                            
                            (("head" id)
                             (when (check-bucket)
                               (format #t "~s\n" (s3-object-head bucket id))))

                            (("delete" id)
                             (when (check-bucket)
                               (format #t "~s\n" (s3-object-delete! bucket id))))

                            (("copy" src dstid)
                             (when (check-bucket)
                               (format #t "~s\n" (s3-object-copy! bucket src dstid))))

                            (("quit") (exit 0))

                            ((cmd . args)
                             (format (current-error-port)
                                     "ERROR: invalid command: ~a\n" cmd))))
                   (loop)))))))

(define (tokens-drop str num)
  (if (= num 0)
      str
      (let1 m (#/[ \t\r\n]+/ str)
        (if m
            (tokens-drop (rxmatch-after m) (- num 1))
            ""))))

;; KLUDGE!  properly support this.
(define (setup-proxy)
  (with-module net.amazon.s3
    (let1 *s3-endpoint* "s3.amazonaws.com"
      (set! prepare-http-headers
            (lambda (method bucket uri headers body)
              (define (ensure-date hdrs)
                (cond [(rfc822-header-ref headers "date") hdrs]
                      [else `(("date" ,(date->rfc822-date (current-date)))
                              ,@hdrs)]))
              (define (ensure-host hdrs)
                (cond [(rfc822-header-ref headers "host") hdrs]
                      [bucket `(("host" ,#`",|bucket|.,*s3-endpoint*") ,@hdrs)]
                      [else   `(("host" ,*s3-endpoint*) ,@hdrs)]))
              (define (ensure-content-length hdrs)
                (cond [(not body) hdrs]
                      [else `(("content-length" ,(x->string (string-size body)))
                              ,@hdrs)]))

              (let1 h ($ ensure-content-length $ ensure-host
                         $ ensure-date headers)
                `(,@(append-map (^p `(,(make-keyword (car p)) ,(cadr p))) h)
                  :authorization ,(s3-auth-header-value
                                   (s3-signature (x->string method)
                                                 h bucket uri)))))))
    (set! *s3-endpoint* "localhost:8080")))

;; Local variables:
;; mode: scheme
;; end:
